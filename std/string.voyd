use std::macros::all
use array::all
use operators::all

fn create_new_string(size: i32) -> string
  binaryen_gc_call(arrayNew, [bin_type_to_heap_type(string), size])

fn copy(dest_str: string, opts: {
  from: string,
  to_index: i32,
  from_index: i32,
  count: i32
}) -> string
  binaryen_gc_call(arrayCopy, [
    dest_str,
    opts.to_index,
    opts.from,
    opts.from_index,
    opts.count
  ])
  dest_str

fn compute_index(index: i32, length: i32) -> i32
  if index < 0 then: length + index else: index

pub fn slice(str: string, start: i32, end: i32) -> string
  let computed_start = compute_index(start, str.length)
  let computed_end = compute_index(end, str.length)

  if (computed_start >= str.length) or (computed_end >= str.length) or (computed_start >= computed_end) then:
    ""
  else:
    let new_length = computed_end - computed_start
    let new_chars = create_new_string(new_length)
    new_chars.copy({
      from: str.chars,
      to_index: 0,
      from_index: start,
      count: new_length
    })

pub fn char_code_at(str: string, index: i32) -> i32
  let computed_index = compute_index(index, str.length)

  if computed_index >= str.length then:
    -1
  else:
    binaryen_gc_call(
      arrayGet,
      [str, computed_index, BnrType<i32>, BnrConst(false)]
    )

pub fn length(str: string) -> i32
  binaryen_gc_call(arrayLen, [str])

pub fn '+'(str: string, other: string) -> String
  let new_length = str.length + other.length
  let new_string = create_new_string(new_length)
  new_string.copy({
    from: str,
    to_index: 0,
    from_index: 0,
    count: str.length
  })
  new_string.copy({
    from: other,
    to_index: str.length,
    from_index: 0,
    count: other.length
  })

obj StringIterator {
  str: string,
  index: i32
}

pub fn new_string_iterator(str: string) -> StringIterator
  StringIterator { str: str, index: 0 }

pub fn read_next_char(iterator: StringIterator) -> i32
  if iterator.index >= iterator.str.length then:
    -1
  else:
    let char = iterator.str.char_code_at(iterator.index)
    iterator.index = iterator.index + 1
    char
